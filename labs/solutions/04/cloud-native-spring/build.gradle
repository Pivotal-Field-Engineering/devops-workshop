buildscript {
    ext {
	   springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/snapshot" }
	    maven { url "https://repo.spring.io/milestone" }
        mavenCentral()
    }
    dependencies {
	    classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0")
	    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'io.spring.dependency-management'

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
    dotGitDirectory = "${project.rootDir}/../../../.."
}

import org.apache.tools.ant.filters.*

processResources {
    filter ReplaceTokens, tokens: [
	    "application.name": project.property("application.name"),
	    "application.description": project.property("application.description"),
        "application.version": project.property("version")
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR1"
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:2.1.2.RELEASE"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    annotationProcessor('org.projectlombok:lombok:1.18.6')
    implementation('org.projectlombok:lombok:1.18.6')
    implementation('javax.xml.bind:jaxb-api:2.4.0-b180725.0427')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-data-rest')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springframework.data:spring-data-rest-hal-browser')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.security:spring-security-config')
    implementation('io.pivotal.spring.cloud:spring-cloud-services-starter-config-client')
    implementation('org.flywaydb:flyway-core:5.2.4')
    implementation('com.zaxxer:HikariCP:3.3.0')
    runtime('com.h2database:h2')
    runtime('mysql:mysql-connector-java:8.0.14')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

repositories {
    maven { url "https://repo.spring.io/plugins-release" }
    maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
    mavenCentral()
}

jar {
    enabled = true
    excludes = ['**/application.yml', 'io/pivotal/CloudNativeSpringApplication*.class']
}

bootJar {
    enabled = true
    classifier = 'exec'
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

tasks.withType(Test) {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        jvmArgs += ["--add-opens", "java.base/java.lang=ALL-UNNAMED"]
    }
}

publishing {
    publications {
        maven(MavenPublication) {
	    groupId 'io.pivotal'
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
